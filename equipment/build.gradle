apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'maven'
apply from: "../scripts/packageOptions.gradle"

android {
    compileSdkVersion SDKVersion
    buildToolsVersion buildToolsVersion

    defaultConfig {
        minSdkVersion minSDKVersion
        targetSdkVersion SDKVersion
        versionCode appVersionCode
        versionName appVersionName

        vectorDrawables.useSupportLibrary true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

kapt {
    generateStubs = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    api project(path: ':usbSerialForAndroid')

    // Kotlin language
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlinVersion"

    // Android support
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"

    // Rest adapter
    api "com.squareup.retrofit2:retrofit:$retrofitVersion"
    api "com.squareup.retrofit2:converter-jackson:$retrofitVersion"
    api "com.squareup.retrofit2:adapter-rxjava:$retrofitVersion"

    // HTTP
    api "com.squareup.okhttp3:okhttp:$okHttpVersion"
    api "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"

    // Reactive Java
    api "io.reactivex:rxjava:$rxJavaVersion"
    api "io.reactivex:rxandroid:$rxAndroidVersion"
    api "com.jakewharton.rxbinding:rxbinding:$rxBinding"
    api "com.jakewharton.rxbinding:rxbinding-design:$rxBinding"

    // Jackson parser
    api "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
    api "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion"
    api "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    api "com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion"

    // UI
    api "com.android.support.constraint:constraint-layout:$constraintVersion"

    // atol driver
    api "ru.softbalance.hardware:atolprinter:$atolPrinterVersion"

    // Testing
    testImplementation 'junit:junit:4.12'

    androidTestImplementation "com.android.support.test:runner:$androidSupportTestVersion"
    androidTestImplementation "com.android.support.test:rules:$androidSupportTestVersion"
    androidTestImplementation "com.android.support:multidex:$multidexVersion"
    androidTestImplementation "com.android.support:multidex-instrumentation:$multidexVersion"

    androidTestImplementation "com.android.support.test.espresso:espresso-intents:$espressoVersion"
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$espressoVersion", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation("com.android.support.test.espresso:espresso-contrib:$espressoVersion") {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            def userHome = 'file://' + System.getProperty('user.dir') + '/maven-repo'
            repository(url: userHome)
            // standard pom settings
            pom.project {
                name 'Fiscal print library'
                description 'Hi-level library 4 printint fiscal receipts via Atol devices etc.'
                url 'https://github.com/Softbalance/equipment'
                groupId 'ru.softbalance'
                artifactId 'equipment'
                packaging 'aar'
                version appVersionName
                licenses {
                    license {
                        name 'Apache license 2.0'
                        url 'https://www.apache.org/licenses/LICENSE-2.0'
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                        id 'sb'
                        name 'Softbalance Android'
                        email 'gk.softbalance@gmail.com'
                    }
                }
            }
        }
    }
}
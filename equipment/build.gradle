apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'maven'

def currentVersion = '1.4.1'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 2
        versionName currentVersion

        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
        incremental false
    }
    dataBinding {
        enabled = true
    }
    lintOptions {
        abortOnError false
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
}

kapt {
    generateStubs = true
}

def supportVers = '25.3.1'
def retrofitVers = '2.1.0'
def okHttpVers = '3.4.1'
def jacksonVers = '2.8.4'
def autoValueVers = '1.3'
def daggerVers = '2.9'
def mockitoVers = '2.4.2'

dependencies {

    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin language
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    // Android support
    compile "com.android.support:appcompat-v7:$supportVers"
    compile "com.android.support:design:$supportVers"
    compile 'com.android.support:multidex:1.0.0'

    // Data binding
    kapt "com.android.databinding:compiler:$androidPluginVersion"

    // Rest adapter
    compile "com.squareup.retrofit2:retrofit:$retrofitVers"
    compile "com.squareup.retrofit2:converter-jackson:$retrofitVers"
    compile "com.squareup.retrofit2:adapter-rxjava:$retrofitVers"

    // HTTP and WS
    compile "com.squareup.okhttp3:logging-interceptor:$okHttpVers"
    compile "com.squareup.okhttp3:okhttp-ws:$okHttpVers"

    // Reactive Java
    compile 'io.reactivex:rxandroid:1.2.0'
    compile 'io.reactivex:rxjava:1.1.5'
    compile 'com.jakewharton.rxbinding:rxbinding-appcompat-v7:0.4.0'

    // Jackson parser
    compile "com.fasterxml.jackson.core:jackson-core:$jacksonVers"
    compile "com.fasterxml.jackson.core:jackson-annotations:$jacksonVers"
    compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVers"
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVers"

    // Dagger
    compile "com.google.dagger:dagger:$daggerVers"
    kapt "com.google.dagger:dagger-compiler:$daggerVers"
    provided 'javax.annotation:jsr250-api:1.0'

    // autovalue
    provided "com.google.auto.value:auto-value:$autoValueVers"
    kapt "com.google.auto.value:auto-value:$autoValueVers"
    kapt "com.ryanharter.auto.value:auto-value-parcel:0.2.5"
    kapt "com.gabrielittner.auto.value:auto-value-with:1.0.0"

    // logging
    compile 'com.jakewharton.timber:timber:4.3.1'

    // UI
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.google.android:flexbox:0.2.3'

    // atol driver
    compile 'ru.softbalance.hardware:atolprinter:9.9.0.5319'

    // Testing
    kaptTest "com.google.dagger:dagger-compiler:$daggerVers"
    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:$mockitoVers"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"
    testCompile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    kaptAndroidTest "com.google.dagger:dagger-compiler:$daggerVers"
    androidTestCompile "org.mockito:mockito-core:$mockitoVers"
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile "com.android.support:support-annotations:$supportVers"
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

uploadArchives {
    repositories {
        mavenDeployer {
            def userHome = 'file://' + System.getProperty('user.dir') + '/maven-repo'
            repository(url: userHome)
            // standard pom settings
            pom.project {
                name 'Fiscal print library'
                description 'Hi-level library 4 printint fiscal receipts via Atol devices etc.'
                url 'https://github.com/Softbalance/equipment'
                groupId 'ru.softbalance'
                artifactId 'equipment'
                packaging 'aar'
                version currentVersion
                licenses {
                    license {
                        name 'Apache license 2.0'
                        url 'https://www.apache.org/licenses/LICENSE-2.0'
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                        id 'sb'
                        name 'Softbalance Android'
                        email 'gk.softbalance@gmail.com'
                    }
                }
            }
        }
    }
}